 R←Test_acre_006(stopFlag batchFlag);⎕TRAP;projectName;ref;M;res;changeFolder;list;fnsName;home;oldContent;newContent
⍝ Check whether acre processes correctly a rename on a help page.
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed
 projectName←'_',1⊃⎕SI

 #.⎕SHADOW projectName
 res←⎕SE.UCMD'acre.CreateProject "',∆TestFolder,projectName,'" #.',projectName,' -keephistory=on'
 →GoToTidyUp res≢'#.',projectName
 M←##.Laguntza
 home←'#.',projectName,'.',1↓projectName                            ⍝ Where does the help system (pages and nodes) live?
 ref←M.CreateStub home
 home←⍎home
 changeFolder←∆TestFolder,projectName,'\.acre\changes\'
 →GoToTidyUp 4≠⍴↑#.FilesAndDirs.Dir changeFolder

 oldContent←home⍎'Copyright'                                        ⍝ First item is "Copyright"
 1 M.HandleTopicProperties ref.n.∆Ref                               ⍝ Inject a ∆TopicProperty page
 →GoToTidyUp 5≠⍴list←↑#.FilesAndDirs.Dir changeFolder
 →GoToTidyUp 2≠home.⎕NC'Copyright'
 fnsName←'#.',projectName,'.',(1↓projectName),'.∆TopicProperties'
 1 M.RenameHelpTopic ref.n.∆Ref 0
 →GoToTidyUp'A_Test'≢1⊃ref.d.tree[0;]                               ⍝ "A_Test" is the name automatically chosen when the test flag is on
 →GoToTidyUp 0≠home.⎕NC'Copyright'
 →GoToTidyUp 2≠home.⎕NC'A_Test'
 list←↑##.FilesAndDirs.Dir changeFolder
 →GoToTidyUp~(⊂##.FilesAndDirs.NormalizePath'changefile'⎕SE.acre.run(⍕home),'.A_Test')∊list
 →GoToTidyUp(⊂##.FilesAndDirs.NormalizePath'changefile'⎕SE.acre.run(⍕home),'.CopyRight')∊list
 →GoToTidyUp~(⊂##.FilesAndDirs.NormalizePath'changefile'⎕SE.acre.run(⍕home),'.∆TopicProperties')∊list
 newContent←home⍎'A_Test'
 →GoToTidyUp oldContent≢newContent

 R←∆OK

∆TidyUp:
 ##.Laguntza.Reset
 ##.FilesAndDirs.RmDir ∆TestFolder,projectName
 #.⎕EX projectName
⍝Done
