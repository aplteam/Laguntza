 R←Test_124(stopFlag batchFlag);⎕TRAP;report;ps;ref
⍝ Exercise the "InjectNewPageStub" method. Inserts as sibling of node selected. See also Test_0123 & Test_0125
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

 ∆CreateStructure1
 ∆CreatePagesSet1

 ps←##.Markdown2Help.CreateParms ⍬
 ps.helpCaption←'TestCases'
 ps.folderName←GetPathForTempHelpProject'Test_124'
 ps.source←#.TestHelp
 ps.trapErrors←1
 ps.visible←1
 ps.reportToSession←0
 ref←1 ##.Markdown2Help.New ps
 ref.n.form.Event,←⊂9955 1
 ∆Process ref

⍝ Setting SelItem[2]←1 has no effect, therefore we need to perform some gymnastics
 ref ##.Markdown2Help.SelectItemInTreeView 2
 ∆Process ref

 →FailsIf 0≠#.TestHelp.Level1_a.⎕NC⊂'Test_01'
 'sibling'##.Markdown2Help.InjectNewPageStub ref 1 1
 →FailsIf 2.1≠#.TestHelp.Level1_a.⎕NC⊂'Test_01'
 →FailsIf 0≠#.TestHelp.Level1_a.⎕NC⊂'Test_02'
 'sibling'##.Markdown2Help.InjectNewPageStub ref 2 1
 →FailsIf 2.1≠#.TestHelp.Level1_a.⎕NC⊂'Test_02'

 R←∆OK

∆TidyUp:
 ##.Markdown2Help.Reset
 ⎕EX'#.TEMP'
