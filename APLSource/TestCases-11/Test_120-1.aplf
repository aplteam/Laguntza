 R←Test_120(stopFlag batchFlag);⎕TRAP;report;ps;ref;result;result2
⍝ Exercise "DeleteTopicOrNode" on a topic. ∆TopicProperties IS effected!
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 ∆CreateStructure1
 ∆CreatePagesSet1
 ∆CreateTopicProperties1_a
 ∆CreateTopicProperties1_a_2

 ps←##.Laguntza.CreateParms ⍬
 ps.helpCaption←'TestCases'
 ps.folderName←GetPathForTempHelpProject'Test_119'
 ps.source←#.TestHelp
 ps.trapErrors←1
 ps.visible←1
 ps.reportToSession←0
 ref←1 ##.Laguntza.New ps
 ref.n.form.Event,←⊂9955 1
 ∆Process ref

⍝ Setting SelItem[4]←1 has no effect, therefore we need to perform some gymnastics
 ref ##.Laguntza.SelectItemInTreeView 4
 ∆Process ref

 result←#.TestHelp.Level1_a.level2_First.∆TopicProperties ⍬
 →FailsIf 3≠↑⍴result
 →FailsIf 2≠#.TestHelp.Level1_a.level2_First.⎕NC'Topic2'
 1 ##.Laguntza.DeleteTopicOrNode ref
 →FailsIf 0≠#.TestHelp.Level1_a.level2_First.⎕NC'Topic2'
 result2←#.TestHelp.Level1_a.level2_First.∆TopicProperties ⍬
 →FailsIf 2≠↑⍴result2
 →FailsIf 0≠(⊂'Topic2')∊result2[;0]

 R←∆OK

∆TidyUp:
 ##.Laguntza.Reset
 ⎕EX'#.TEMP'
