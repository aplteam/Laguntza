 R←Test_acre_004(stopFlag batchFlag);⎕TRAP;projectName;ref;M;res;changeFolder;list;bodyOld;filename;fnsName;bodyNew
⍝ Is the implicit TopicProperty page change noticed by acre when a page is deleted?
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 projectName←'_',1⊃⎕SI
 #.⎕SHADOW projectName
 res←⎕SE.UCMD'acre.CreateProject "',∆TestFolder,projectName,'" #.',projectName,' -messages=off'
 →GoToTidyUp res≢'#.',projectName
 M←##.Markdown2Help
 ref←M.CreateStub'#.',projectName,'.',1↓projectName
 changeFolder←∆TestFolder,projectName,'\.acre\changes\'
 →GoToTidyUp 4≠⍴↑#.FilesAndDirs.Dir changeFolder

 1 M.HandleTopicProperties ref.n.∆Ref                               ⍝ Inject a ∆TopicProperty page
 →GoToTidyUp 5≠⍴list←↑#.FilesAndDirs.Dir changeFolder
 fnsName←'#.',projectName,'.',(1↓projectName),'.∆TopicProperties'
 filename←'sourcefile'⎕SE.acre.run fnsName
 bodyOld←##.APLTreeUtils.ReadUtf8File filename                      ⍝ Current status of ∆TopicProperty
 1 M.DeleteTopicOrNode ref.n.∆Ref                                   ⍝ Delete currently selected page
 →GoToTidyUp 5≠⍴list←↑#.FilesAndDirs.Dir changeFolder
 bodyNew←##.APLTreeUtils.ReadUtf8File filename                      ⍝ New (updated) status of ∆TopicProperty
 →GoToTidyUp bodyOld≡bodyNew
 →GoToTidyUp∨/'''Copyright'''⍷bodyNew

 R←∆OK

∆TidyUp:
 ##.Markdown2Help.Reset
 #.⎕EX projectName
 ##.FilesAndDirs.RmDir ∆TestFolder,projectName
⍝Done
