 R←Test_Link_009(stopFlag batchFlag);⎕TRAP;ref;html;filename;brokenLinks;ambiguousLinks;has
⍝ Test for a  link that is relative but qualfies fully.
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 #.⎕SHADOW'TestHelp'
 'TestHelp'#.⎕NS''
 'Sub1'#.TestHelp.⎕NS''
 #.TestHelp.Sub1.Hello←'# Sub 1' '' '...'
 'Sub2'#.TestHelp.⎕NS''
 #.TestHelp.Sub2.Hello←'# Sub 1' '' '...'
 'Sub3'#.TestHelp.⎕NS''
 #.TestHelp.Sub3.Page←''
 #.TestHelp.Sub3.Page,←⊂'# A Topic'
 #.TestHelp.Sub3.Page,←⊂''
 #.TestHelp.Sub3.Page,←⊂'This link →[##.Sub2.Hello] is not ambiguous'
 ref←StartHelp 0        ⍝ Default

 ∆Process ref
 brokenLinks←¯1 ##.Laguntza.Lint_.CheckBrokenLinks ref
 →GoToTidyUp~0∊⍴brokenLinks                                  ⍝ We expect no broken links
 ambiguousLinks←¯1 ##.Laguntza.Lint_.CheckAmbiguousLinks ref
 →GoToTidyUp~0∊⍴ambiguousLinks                                ⍝ We expect no(!) ambiguous link
 filename←##.Laguntza.BuildDocFromAllPages ref
 html←'flat'#.APLTreeUtils.ReadUtf8File filename
 has←+/¨html∘{+/⍵⍷⍺}¨'<em>' '</em>'
 →GoToTidyUp 0≠+/has
 has←+/¨html∘{+/⍵⍷⍺}¨'<strong>' '</strong>'
 →GoToTidyUp 0≠+/has

 R←∆OK
∆TidyUp:
 ##.Laguntza.Reset
 ⎕EX'#.TestHelp'
