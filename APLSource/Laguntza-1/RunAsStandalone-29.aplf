 RunAsStandalone;ps;ref;⎕ML;⎕IO;cd;msg;filename;rc;folderName;expand;tieNo;⎕TRAP;parms
⍝ This function runs under ViewHelp.exe.
⍝ It will be moved to #; therefore all calls must be fully qualified.
 ⎕ML←3 ⋄ ⎕IO←0
 ⎕TRAP←(0 'E' '#.Laguntza.HandleError')
 ⍝AllowRide 4534 1
 :If 0=A.IsDevelopment
     folderName←#.Laguntza.GetHelpFolder ⍬
     expand←'None'
     filename←folderName,'\',#.Laguntza.File.GetHelpFileName
     :If ~#.Laguntza.File.DoesFileExist filename
         msg←'Could not find:'filename
         msg,←'' 'The programm cannot continue.'
         'Laguntza'#.Laguntza.Show msg
         ⎕OFF
     :EndIf
     tieNo←filename ⎕FSTIE 0
     parms←⎕FREAD tieNo 6
     ⎕FUNTIE tieNo
     ps←⎕NS''
     parms[parms[;0]⍳⊂'userMode';1]←1 ⍝ This function always runs in user mode
     :If ~0∊⍴parms
         ps.{⍎(0⊃⍵),'←1⊃⍵'}¨↓parms
     :EndIf
     ps.folderName←folderName
     :If #.Laguntza.File.DoesFileExist'Files\help.ico'
         ps.helpIcon←'file:///',(↑⎕CMD'cd'),'\Files\help.ico'
     :EndIf
     ps.visible←1
     ps.page←#.Laguntza.GetPageName ⍬
     ref←#.Laguntza.New ps
     ⎕DQ ref
     ⎕OFF
 :Else
     ⎕←'*** Runs under Runtime ViewHelp.exe only!'
 :EndIf
