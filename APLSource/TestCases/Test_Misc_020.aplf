 R←Test_Misc_020(stopFlag batchFlag);⎕TRAP;ref;ps;css
⍝ Test with an empty namespace: cannot work of course, so we expect an error.
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'TestHelp'
 'TestHelp'#.⎕NS''

 ps←##.Laguntza.CreateParms ⍬
 ps.helpCaption←'TestCases'
 ps.folderName←GetPathForTempHelpProject↑¯1↑'.'A.Split 0⊃⎕XSI
 ps.source←#.TestHelp
 ps.regPath←0

 :Trap 11
     ref←1 ##.Laguntza.New ps
     →T.GoToTidyUp 1
 :Else
     →T.GoToTidyUp'The source namespace is empty!'≢↑⎕DM
 :EndTrap

 R←T._OK

∆TidyUp:
 ##.Laguntza.Reset
 :Trap 0 ⋄ F.RmDir ps.folderName ⋄ :EndTrap
⍝Done
