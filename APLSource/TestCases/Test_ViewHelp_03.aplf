 R←Test_ViewHelp_03(stopFlag batchFlag);⎕TRAP;ref;report;path;tempPath;processInfo;result;more;parms;pid;flag;success;zipFilename
⍝ Test whether image shows or not. Was once a bug.
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If batchFlag
     R←T._NoBatchTest
     :Return
 :EndIf

 R←T._Failed

 path←F.NormalizePath F.PWD,'\Dist\'
 tempPath←F.GetTempSubDir''

 zipFilename←path,'Laguntza-',({⍵/⍨2≥+\⍵='.'}1⊃##.Laguntza.Version),'.zip'
 zipFilename ##.Laguntza.DotNetZip.UnzipTo tempPath
 'Create!'F.CheckPath tempPath,'/help/files/Images'
 (tempPath,'/help/files/Images/')⎕NCOPY F.PWD,'\FlipDB-Team.png'

 parms←Execute.DefaultParms
 parms.wait←0
 (rc processInfo result more)←parms Execute.Application tempPath,'/ViewHelp.exe page="Images"'
 'Something went terribly wrong'Assert 0=rc
 pid←2⊃processInfo

 R←~1 ∆YesOrNo'Does the image "FlipDB-Team" show up?'
 {}OS.KillPID pid
 →T.GoToTidyUp R

∆TidyUp:
 F.RmDir tempPath
⍝Done
