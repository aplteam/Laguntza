 R←Test_ViewHelp_03(stopFlag batchFlag);⎕TRAP;ref;report;path;tempPath;rc;processInfo;result;more;parms;pid;flag;myZipper;success
⍝ Test whether image shows or not. Was once a bug.
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If batchFlag
     R←T._NoBatchTest
     :Return
 :EndIf

 R←T._Failed

 path←##.FilesAndDirs.NormalizePath ##.FilesAndDirs.PWD,'\Dist\'
 tempPath←##.FilesAndDirs.GetTempSubDir''

 myZipper←⎕NEW ##.SevenZip(,⊂path,'Laguntza-',({⍵/⍨2≥+\⍵='.'}1⊃##.Laguntza.Version),'.zip')
 rc←myZipper.Unzip tempPath
 'Something went terribly wrong'Assert 0=↑rc
 'Create!'##.FilesAndDirs.CheckPath tempPath,'/help/files/Images'
 (##.FilesAndDirs.PWD,'\FlipDB-Team.png')##.FilesAndDirs.CopyTo tempPath,'/help/files/Images/'

 parms←##.Execute.DefaultParms
 parms.wait←0
 (rc processInfo result more)←parms ##.Execute.Application tempPath,'/ViewHelp.exe page="Images"'
 'Something went terribly wrong'Assert 0=rc
 pid←2⊃processInfo

 R←~1 ∆YesOrNo'Does the image "FlipDB-Team" show up?'
 {}##.OS.KillPID pid
 →T.GoToTidyUp R

∆TidyUp:
 ##.FilesAndDirs.RmDir tempPath
⍝Done
