 R←Test_SALT_001(stopFlag batchFlag);⎕TRAP;parms;ref;buff;name;list;saltHandler;qed;acreHandler
⍝ Create a help system that is managed (saved) by SALT, delete and and then restore from SALT files.
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 saltHandler←##.Laguntza.CheckOnEditFixForSaltHandler ⍬
 qed←'⎕SE'⎕WG'editor'
 acreHandler←'⎕SE.acre.'{⍺≡(≢⍺)↑' '~⍨⍕⍵}qed.onFix

 ⎕SE.SALTUtils.EditorFix'Start'

 ⎕EX'#.TestHelp'
 parms←##.Laguntza.CreateParms ⍬
 name←##.FilesAndDirs.GetTempFilename''
 ##.FilesAndDirs.DeleteFile name
 parms.saltFolder←¯4↓name
 ref←parms ##.Laguntza.CreateStub'#.TestHelp'
 ⎕DL 0.2
 ref.Close
 list←↑('recursive' 1)##.FilesAndDirs.Dir parms.saltFolder,'\*'
 →T.GoToTidyUp 5≠≢list
 R←T._OK

∆TidyUp:
 ##.Laguntza.Reset
 ⎕EX'#.TestHelp'
 #.⎕EX'SALT_Var_Data'
 :If acreHandler
     {}⎕SE.acre._code.editFix'Start'
 :ElseIf 0=saltHandler
     qed.onFix←0
 :EndIf
