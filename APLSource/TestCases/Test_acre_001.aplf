 R←Test_acre_001(stopFlag batchFlag);⎕TRAP;projectName;ref;L;res;folder;list
⍝ Is adding a new page noticed by acre?
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 projectName←'_',0⊃⎕SI
 #.⎕SHADOW projectName
 res←⎕SE.UCMD'acre.CreateProject "',∆TestFolder,projectName,'" #.',projectName
 →T.GoToTidyUp res≢'#.',projectName
 folder←∆TestFolder,projectName,'\APLSource\'
 →T.GoToTidyUp 1≠≢list←↑('recursive' 1)F.Dir folder
 L←##.Laguntza
 ref←L.CreateStub'#.',projectName,'.',1↓projectName
 →T.GoToTidyUp 7≠≢list←↑('recursive' 1)F.Dir folder

 L.InjectNewPageStub ref.n.∆Ref 0 1
 →T.GoToTidyUp 8≠≢list←↑('recursive' 1)F.Dir folder

 R←T._OK

∆TidyUp:
 ##.Laguntza.Reset
 #.⎕EX projectName
 F.RmDir ∆TestFolder,projectName
⍝Done
