 R←Test_Search_003(stopFlag batchFlag);⎕TRAP;ref
⍝ Test whether the link text is considered in a search operation
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'TestHelp'
 'TestHelp'#.⎕NS''
 #.TestHelp.(Page Page2)←⊂''
 #.TestHelp.Page,←⊂'# Foo'
 #.TestHelp.Page2,←⊂'Some paragraph.'
 #.TestHelp.Page2,←⊂''
 #.TestHelp.Page2,←⊂'For details see →[*.Page | Firlefanz]'
 ref←StartHelp 0

 1 ⎕NQ ref.n.search'Select'
 ref.n.fullTextSearchFlag.State←1
 ref.n.searchBtn.Active←1
 ref.n.searchString.Text←'Firlefanz'
 1 ⎕NQ ref.n.searchBtn'Select'
 →T.GoToTidyUp 1≠≢ref.n.reportList.Items
 →T.GoToTidyUp(,⊂'Page2')≢ref.n.reportList.Items

 R←T._OK

∆TidyUp:
 ##.Laguntza.Reset
⍝Done
