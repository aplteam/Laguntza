 R←Test_acre_003(stopFlag batchFlag);⎕TRAP;projectName;ref;L;res;aplsourceFolder;list;bodyOld;filename;fnsName;bodyNew
⍝ Is the implicit TopicProperty page change noticed by acre when a page is added?
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW projectName←0⊃⎕SI
 res←⎕SE.UCMD'acre.CreateProject "',∆TestFolder,'\',projectName,'" #.',projectName
 →T.GoToTidyUp res≢'#.',projectName
 L←##.Laguntza

 ref←L.CreateStub'#.',projectName,'.',projectName
 aplsourceFolder←∆TestFolder,'\',projectName,'\APLSource\'
 →T.GoToTidyUp 7≠≢list←↑('recursive' 1)F.Dir aplsourceFolder

 1 L.HandleTopicProperties ref.n.∆Ref                           ⍝ Inject a ∆TopicProperty page
 →T.GoToTidyUp 8≠≢list←↑('recursive' 1)F.Dir aplsourceFolder
 fnsName←'#.',projectName,'.',projectName,'.∆TopicProperties'
 filename←↑⎕SE.acre.Sourcefile fnsName
 bodyOld←↑F.NGET filename                                       ⍝ Current status of ∆TopicProperty
 L.InjectNewPageStub ref.n.∆Ref 0 1                             ⍝ Inject a new help page
 →T.GoToTidyUp 9≠≢list←↑('recursive' 1)F.Dir aplsourceFolder
 bodyNew←↑F.NGET filename                                       ⍝ New (updated) status of ∆TopicProperty
 →T.GoToTidyUp bodyOld≡bodyNew

 R←T._OK

∆TidyUp:
 ##.Laguntza.Reset
 F.RmDir ∆TestFolder,'\',projectName
⍝Done
