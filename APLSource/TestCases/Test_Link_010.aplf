 R←Test_Link_010(stopFlag batchFlag);⎕TRAP;ref;html;filename;brokenLinks;ambiguousLinks;has
⍝ Test for a fully qualified internal link.
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'TestHelp'
 'TestHelp'#.⎕NS''
 'Sub1'#.TestHelp.⎕NS''
 #.TestHelp.Sub1.Hello←'# Sub 1' '' '...'
 'Sub2'#.TestHelp.⎕NS''
 #.TestHelp.Sub2.Hello←'# Sub 2' '' '...'
 'Sub3'#.TestHelp.⎕NS''
 #.TestHelp.Sub3.Page←''
 #.TestHelp.Sub3.Page,←⊂'# A Topic'
 #.TestHelp.Sub3.Page,←⊂''
 #.TestHelp.Sub3.Page,←'This link →[#.TestHelp.Sub2.Hello |' 'Link text] has a line break in between'
 ref←StartHelp 0        ⍝ Default

 ∆Process ref
 brokenLinks←¯1 ##.Laguntza.Lint_.CheckBrokenLinks ref
 →T.GoToTidyUp 0≠≢brokenLinks                                  ⍝ We expect no broken links
 ambiguousLinks←¯1 ##.Laguntza.Lint_.CheckAmbiguousLinks ref
 →T.GoToTidyUp 0≠≢ambiguousLinks                                ⍝ We expect no ambiguous link
 filename←##.Laguntza.BuildDocFromAllPages ref
 html←↑F.NGET filename
 has←+/¨html∘{+/⍵⍷⍺}¨'<em>' '</em>'
 →T.GoToTidyUp 0≠+/has
 has←+/¨html∘{+/⍵⍷⍺}¨'<strong>' '</strong>'
 →T.GoToTidyUp 0≠+/has

 R←T._OK
∆TidyUp:
 ##.Laguntza.Reset
⍝Done
