 R←Test_001(stopFlag batchFlag);⎕TRAP;ref;buff;html;filename
⍝ Test Laguntza's own help system
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 ref←##.Laguntza.Selfie ⍬
 ref.p.reportToSession←0
 ##.FilesAndDirs.DeleteFile ref.∆tempFilename

 filename←##.Laguntza.GUI.OnBuildDocFromAllPages ref
 html←##.APLTreeUtils.ReadUtf8File filename
 →T.GoToTidyUp 0<+/'<span'⍷∊html                          ⍝ Invalid links would be marked up by <span

 buff←##.Laguntza.Lint_.{A←##.##.APLTreeUtils ⋄ ¯1 CheckBrokenLinks ⍵}ref
 →T.GoToTidyUp 0≠≢buff                                    ⍝ There should be no broken links of course

 buff←##.Laguntza.Lint_.{A←##.##.APLTreeUtils ⋄ ¯1 CheckAmbiguousLinks ⍵}ref
 →T.GoToTidyUp 0≠≢buff                                    ⍝ There should be no ambiguous links of course

 buff←##.Laguntza.Lint_.ReportTopicProperties ref
 →T.GoToTidyUp 0≠≢buff                                    ⍝ There should be nothing to report

 buff←##.Laguntza.ReportMissingIndexEntries ref
 →T.GoToTidyUp'All help pages carry index entries'≢buff

 R←T._OK

∆TidyUp:
 ##.Laguntza.Reset
 :Trap 0 ⋄ ##.FilesAndDirs.DeleteFile filename ⋄ :EndTrap
⍝Done
