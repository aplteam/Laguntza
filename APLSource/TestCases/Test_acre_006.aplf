 R←Test_acre_006(stopFlag batchFlag);⎕TRAP;projectName;ref;L;res;aplsourceFolder;list;fnsName;home;oldContent;newContent
⍝ Check whether acre processes correctly a rename on a help page.
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW projectName←0⊃⎕SI
 res←⎕SE.UCMD'acre.CreateProject "',∆TestFolder,'\',projectName,'" #.',projectName
 →T.GoToTidyUp res≢'#.',projectName
 L←##.Laguntza

 home←'#.',projectName,'.',projectName                              ⍝ Where does the help system (pages and nodes) live?
 ref←L.CreateStub home
 home←⍎home
 aplsourceFolder←∆TestFolder,'\',projectName,'\APLSource\'
 →T.GoToTidyUp 7≠≢list←↑('recursive' 1)##.FilesAndDirs.Dir aplsourceFolder

 oldContent←home⍎'Copyright'                                        ⍝ First item is "Copyright"
 1 L.HandleTopicProperties ref.n.∆Ref                               ⍝ Inject a ∆TopicProperty page
 →T.GoToTidyUp 8≠≢list←↑('recursive' 1)##.FilesAndDirs.Dir aplsourceFolder
 →T.GoToTidyUp 2≠home.⎕NC'Copyright'
 fnsName←'#.',projectName,'.',projectName,'.∆TopicProperties'
 1 L.RenameHelpTopic ref.n.∆Ref 0
 →T.GoToTidyUp'A_Test'≢1⊃ref.d.tree[0;]                               ⍝ "A_Test" is the name automatically chosen when the test flag is on
 →T.GoToTidyUp 0≠home.⎕NC'Copyright'
 →T.GoToTidyUp 2≠home.⎕NC'A_Test'
 list←↑('recursive' 1)##.FilesAndDirs.Dir aplsourceFolder
 →T.GoToTidyUp~(⊂##.FilesAndDirs.NormalizePath↑⎕SE.acre.Sourcefile(⍕home),'.A_Test')∊list
 →T.GoToTidyUp(⊂##.FilesAndDirs.NormalizePath↑⎕SE.acre.Sourcefile(⍕home),'.CopyRight')∊list
 →T.GoToTidyUp~(⊂##.FilesAndDirs.NormalizePath↑⎕SE.acre.Sourcefile(⍕home),'.∆TopicProperties')∊list
 newContent←home⍎'A_Test'
 →T.GoToTidyUp oldContent≢newContent

 R←T._OK

∆TidyUp:
 ##.Laguntza.Reset
 ##.FilesAndDirs.RmDir ∆TestFolder,'\',projectName
⍝Done
