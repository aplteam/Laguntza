 R←Test_ViewHelp_01(stopFlag batchFlag);⎕TRAP;ref;report;path;tempPath;rc;processInfo;result;more;parms;pid;flag;success;zipFilename
⍝ Test `ViewHelp.exe`. You must run ]runmake first for this to test the right version!
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If batchFlag
     R←T._NoBatchTest
     :Return
 :EndIf

 path←'expand'F.NormalizePath ##.CiderConfig.HOME,'\Dist\'
 tempPath←F.GetTempSubDir''

 Assert 1=≢zipFilename←F.ListFiles2 path,'*.zip'
 zipFilename←↑zipFilename

 rc←zipFilename ##.Laguntza.DotNetZip.UnzipTo tempPath
 tempPath ⎕NCOPY⍠('IfExists' 'Replace')⊣##.CiderConfig.HOME,'\Dist\ViewHelp.exe'

 parms←Execute.DefaultParms
 parms.wait←0
 (rc processInfo result more)←parms Execute.Application tempPath,'\ViewHelp.exe'
 'Something went terribly wrong'Assert 0=rc
 pid←2⊃processInfo
 Assert pid∊(GetAllTasks ⍬)[;1]
 {}OS.KillPID pid
 Assert~pid∊(GetAllTasks ⍬)[;1]

 R←T._OK

∆TidyUp:
 F.RmDir tempPath
⍝Done
