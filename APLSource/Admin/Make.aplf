 Make;home;targetPath;C;myUCMDsFolder;name;tempFolder;M;rc;zipFilename;parms2;parms;cfg;msg
⍝ 5.3.0 - 2024-11-30
 :If ##.TestCases.CommTools.YesOrNo'Sure you want to build "Laguntza"? The project will be closed and the WS destroyed in the process!'
     :If 0=⎕SE.⎕NC'MakeHelpers' ⋄ {}⎕SE.Tatin.LoadPackages'[tatin]MakeHelpers'⎕SE ⋄ :EndIf
    ⍝ --- Preparation ---------------------------------------
     C←##.CiderConfig
     home←C.HOME,'\'
     targetPath←home,'Dist\'
     M←⎕SE.MakeHelpers
     myUCMDsFolder←M.GetMyUCMDsFolder'\'
     name←'Laguntza'
     tempFolder←(M.##.F.GetTempSubDir name),'\'
    ⍝ --- Version number
     M.IncreaseBuildIDInFunction'#.Laguntza.Laguntza.Version'
     cfg←M.GetPackageCfg ##.CiderConfig.HOME,'/apl-package.json'
     :If ≢/M.DropBuildNumber¨(2⊃#.Laguntza.Laguntza.Version)cfg.version
         msg←''
         msg,←⊂'Cannot run because there is a problem with the version number:'
         msg,←⊂'#.Laguntza.Laguntza.Version: ',M.DropBuildNumber 2⊃#.Laguntza.Laguntza.Version
         msg,←⊂'Package config file:         ',M.DropBuildNumber cfg.version
         msg←↑msg
         msg←'*'⍪msg⍪'*'
         msg←'*',msg,'*'
         ⎕←msg
         :Return
     :EndIf
    ⍝ --- Create the WS --------------------------------
     M.RecreateFolder targetPath
     {}⎕SE.Tatin.CreateAPIfromCFG ##.Laguntza cfg
     {}⎕SE.Cider.CloseProject''                       ⍝ Precaution
     ⎕WSID←tempFolder,name,'.dws'
     ⎕EX¨'M' 'cfg'
     0 ⎕SAVE ⎕WSID
     #.⎕EX ⎕NL⍳16
     {⍵:.}' '≠1↑0⍴⎕FX↑'r←M' 'r←⎕SE.MakeHelpers'       ⍝ avoids creating a link in # that points to ⎕SE
     ⎕LX←'#.',name,'.RunAsStandalone'
     '#.Laguntza.Laguntza'#.⎕CY ⎕WSID
     {}2 ⎕SE.Tatin.LoadDependencies(home,'packages\')#.Laguntza  ⍝ Yes, this IS correct: we reorganize the WS
     0 ⎕SAVE ⎕WSID
    ⍝ --- Copy stuff around
     (home,'Assets\CSS')M.CopyTo tempFolder,'Assets\'
     (home,'Assets\Images')M.CopyTo tempFolder,'Assets\'
    ⍝ --- Create the stand-alone-EXE ------------------------
     1 ⎕NDELETE home,'\ViewHelp.exe'
     parms←M.CreateStandAloneExeParms(targetPath,'ViewHelp.exe')'#.Laguntza'
     parms.CompanyName←'Kai Jaeger'
     parms.Icon←tempFolder,'Assets\Images\ViewHelp.ico'
     parms.ProjectName←parms.ProductName←name
     {}M.MakeStandAloneRunTimeExe parms
     (targetPath,'ViewHelp.exe')M.CopyTo home,'\'
    ⍝ --- Create the package --------------------------------
     'CiderConfig'#.⎕NS''                    ⍝ This is required by ...
     #.CiderConfig.HOME←home                 ⍝ ... CompileHelpFileInto
     #.Laguntza.HelpHelp #.Laguntza.CompileHelpFileInto tempFolder  ⍝ !
     (C.HOME,'\')⎕NCOPY⍠('IfExists' 'Replace')⊣tempFolder,'Laguntza.dcf'
     parms2←⎕SE.Tatin.CreateBuildParms C.HOME
     parms2.targetPath←tempFolder
     parms2.version←2⊃#.Laguntza.Version
     zipFilename←⎕SE.Tatin.BuildPackage parms2
     {⍵:0 0⍴⍬ ⋄ 'Something went wrong'⎕SIGNAL 11}0<≢zipFilename
     zipFilename M.CopyTo targetPath
    ⍝ --- Cleaning up ---------------------------------------
     {}M.##.F.RmDirByForce tempFolder
     ⎕←'Clearing the workspace...'
     ⎕CLEAR
 :EndIf
