 {r}←∆Ref DeleteAPL_Object name;result
 r←⍬
 :If ∆Ref.p.notifyLink
     :If 9=⎕SE.⎕NC'Link'
         result←⎕SE.Link.Expunge name
         :If ∆Ref.p.reportToSession∧0=≢result
             ⎕←'*** Link was told that "',(Laguntza.A.Last name),'" was deleted but wasn''t interested'
         :EndIf
     :Else
         result←'No Link anywhere'
     :EndIf
 :EndIf
 :If ∆Ref.p.notifyAcre
     :If 9=⎕SE.⎕NC'acre'
         result←⎕SE.UCMD'acre.Erase ',name
     :ElseIf 9=#.⎕NC'acre'
         result←#.acre.Erase name
     :Else
         result←'No acre anywhere'
     :EndIf
     :If ∆Ref.p.reportToSession∧0=≢result
         ⎕←'*** acre was told that "',(Laguntza.A.Last name),'" was deleted but wasn''t interested'
     :EndIf
 :EndIf
 ⎕EX name
⍝Done
